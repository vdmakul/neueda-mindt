buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.5.RELEASE")
    }
}
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'spring-boot'

version = '0.1'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

mainClassName = "lv.vdmakul.mindt.console.ConsoleApp"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--format', 'pretty', '--glue', 'lv.vdmakul.mindt.service.cucumber', System.properties['mindt.features']]
        }
    }
}

dependencies {
    compile 'org.springframework:spring-web:4.1.0.RELEASE'
    compile 'com.google.code.gson:gson:2.3'
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    testCompile 'info.cukes:cucumber-java:1.1.8'
    testCompile 'info.cukes:cucumber-junit:1.1.8'

    compile 'org.springframework.boot:spring-boot-starter-web'

    compile 'org.slf4j:slf4j-api:1.7.7'


    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.springframework:spring-test:4.1.0.RELEASE'
}